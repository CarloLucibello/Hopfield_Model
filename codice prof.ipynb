{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "7a27e004",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "run_mcmc (generic function with 1 method)"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Random, Statistics, LinearAlgebra\n",
    "using Plots\n",
    "using BenchmarkTools\n",
    "using Test\n",
    "\n",
    "function generate_hopfield_sample(N, α)\n",
    "    M = round(Int, N*α)\n",
    "    ξ = rand([-1,1], N, M)\n",
    "    J = (ξ * ξ') ./ N\n",
    "    return ξ, J\n",
    "end\n",
    "\n",
    "rand_config(N::Int) = rand([-1,1], N)\n",
    "\n",
    "\"\"\"\n",
    "Flip each spin of `σ` with probability `p`.\n",
    "\"\"\"\n",
    "function perturb_config(σ::AbstractVector, p)\n",
    "    @assert 0 <= p <= 1\n",
    "    σnew = copy(σ)\n",
    "    for i in 1:length(σnew)\n",
    "        if rand() < p\n",
    "            flip!(σnew, i)\n",
    "        end \n",
    "    end\n",
    "    return σnew\n",
    "end\n",
    "\n",
    "function energy(J::AbstractMatrix, σ::AbstractVector)\n",
    "    return -(σ' * J * σ) / 2\n",
    "end\n",
    "\n",
    "function staggered_mag(ξ::AbstractVector, σ::AbstractVector)\n",
    "    return ξ ⋅ σ / length(σ)\n",
    "end\n",
    "\n",
    "function staggered_mags(ξ::AbstractMatrix, σ::AbstractVector)\n",
    "    return ξ' * σ ./ length(σ)\n",
    "end\n",
    "\n",
    "function is_local_minimum(J, σ)\n",
    "    h = J * σ .- diag(J) .* σ\n",
    "    return σ == sign.(h) \n",
    "end\n",
    "\n",
    "flip!(σ, i::Int) = σ[i] = -σ[i]\n",
    "\n",
    "function delta_energy(J, σ, i::Int)\n",
    "    hi = @views J[:, i] ⋅ σ  - J[i, i] * σ[i]  # this dot product dominates the compute time of the whole simulation\n",
    "    return 2hi * σ[i]\n",
    "end\n",
    "\n",
    "function one_mc_step!(J, σ, i, T)\n",
    "    flipped = false\n",
    "    ΔE = delta_energy(J, σ, i)\n",
    "    # ΔE = randn()\n",
    "    if ΔE < 0 || rand() < exp(-ΔE / T)\n",
    "        flip!(σ, i)\n",
    "        flipped = true\n",
    "    end\n",
    "    #if rand() < exp(-ΔE / T)\n",
    "     #   flip!(σ, i)\n",
    "      #  flipped = true\n",
    "    #end\n",
    "    return flipped\n",
    "end\n",
    "\n",
    "function one_mc_sweep!(J, σ, T)\n",
    "    N = length(σ)\n",
    "    nflips = 0\n",
    "    for _ in 1:N\n",
    "        i = rand(1:N)\n",
    "        nflips += one_mc_step!(J, σ, i, T)  \n",
    "    end\n",
    "    return nflips / N \n",
    "end\n",
    "\n",
    "function run_mcmc(J::AbstractMatrix;  \n",
    "            σ0::AbstractVector = rand([-1,1], size(J, 1)), \n",
    "            T=1e-6,\n",
    "            nsweeps=100, \n",
    "            infotime=0, # Set to 0 for no printing\n",
    "            earlystop=0, # Stop if fliprate is equal or less than this threshold.\n",
    "                         # Set to negative for no early stopping\n",
    "            )\n",
    "    \n",
    "    N = size(J, 1)\n",
    "    @assert length(σ0) == N\n",
    "    @assert all(∈([-1,1]), σ0)\n",
    "    σ = copy(σ0)\n",
    "\n",
    "    function report(t, fliprate)\n",
    "        E = energy(J, σ) / N\n",
    "        @info (; t, E, fliprate)\n",
    "    end\n",
    "\n",
    "    infotime > 0 && report(0, missing)\n",
    "    for t in 1:nsweeps\n",
    "        fliprate = one_mc_sweep!(J, σ, T)\n",
    "        if infotime > 0 && t % infotime == 0\n",
    "            report(t, fliprate)\n",
    "        end\n",
    "        fliprate <= earlystop && break\n",
    "    end\n",
    "    return σ\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "e2422ee4",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      " 10.715879 seconds (19 allocations: 400.696 MiB, 0.50% gc time)\n",
      "0"
     ]
    }
   ],
   "source": [
    "# Random.seed!(17)\n",
    "c = 0\n",
    "@time begin\n",
    "    for _ in 1:1\n",
    "        N = 5000  \n",
    "        α = 0.1\n",
    "        p = 0.4 # perturb probability\n",
    "        ξ, J = generate_hopfield_sample(N, α)\n",
    "        σ0 = perturb_config(ξ[:,1], p)\n",
    "\n",
    "        #print(staggered_mag(σ0, ξ[:,1]), \"\\n\")\n",
    "        σ = run_mcmc(J; σ0, nsweeps=100, T=1e-6)\n",
    "        #print(staggered_mag(σ, ξ[:,1]), \"\\n\")\n",
    "        m = staggered_mags(ξ, σ)\n",
    "        if m[1] >= 0.95\n",
    "            c +=1\n",
    "        end\n",
    "    end\n",
    "end\n",
    "#print( staggered_mag(ξ[:, 1], σ), \"\\n\")\n",
    "print(c)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ad035536",
   "metadata": {},
   "outputs": [],
   "source": [
    "#### BENCHMARK && PROFILING ###############\n",
    "\n",
    "# σ0 = rand([-1, 1], N)\n",
    "# @btime run_mcmc(J; σ0, nsweeps=10, earlystop=-1, infotime=0)\n",
    "## N=100, α = 0.1, on my laptop:\n",
    "## 38.567 μs (4 allocations: 1.05 KiB) \n",
    "\n",
    "# VSCodeServer.@profview run_mcmc(J; σ0, nsweeps=10, earlystop=-1, infotime=0)\n",
    "## All time spent in dot() for computing hi, so cannot do anything to improve performance\n",
    "\n",
    "### TESTS BELOW ##############\n",
    "# TODO move to separate file\n",
    "\n",
    "@testset \"delta_energy\" begin\n",
    "    N = 100\n",
    "    α = 0.1\n",
    "    ξ, J = generate_hopfield_sample(N, α)\n",
    "    \n",
    "    σ = rand([-1, 1], N)\n",
    "    i = rand(1:N)\n",
    "    \n",
    "    E0 = energy(J, σ)\n",
    "    flip!(σ, i)\n",
    "    E1 = energy(J, σ)\n",
    "    flip!(σ, i)\n",
    "    @test delta_energy(J, σ, i) ≈ E1 - E0\n",
    "end\n",
    "\n",
    "@testset \"at high temp accept every moove\" begin\n",
    "    N = 100\n",
    "    α = 0.1\n",
    "    ξ, J = generate_hopfield_sample(N, α)\n",
    "    \n",
    "    T = Inf\n",
    "    σ = rand([-1, 1], N)\n",
    "    fliprate = one_mc_sweep!(J, σ, T)\n",
    "    @test fliprate == 1\n",
    "\n",
    "    T = 10\n",
    "    σ = rand([-1, 1], N)\n",
    "    fliprate = one_mc_sweep!(J, σ, T)\n",
    "    @test fliprate > 0.95\n",
    "\n",
    "    T = 1\n",
    "    σ = rand([-1, 1], N)\n",
    "    fliprate = one_mc_sweep!(J, σ, T)\n",
    "    @test 0.5 < fliprate < 0.9\n",
    "end\n",
    "\n",
    "@testset \"patterns are fixed points at low alpha\" begin\n",
    "    N = 100\n",
    "    α = 0.1\n",
    "    ξ, J = generate_hopfield_sample(N, α)\n",
    "    @test is_local_minimum(J, ξ[:,1])\n",
    "    \n",
    "    σ0 = ξ[:,1]\n",
    "    σ = run_mcmc(J; σ0, nsweeps=3, infotime=100)\n",
    "    @test σ == σ0\n",
    "    @test σ !== σ0\n",
    "    m = staggered_mags(ξ, σ)\n",
    "    @test m[1] == 1\n",
    "end   \n",
    "\n",
    "\n",
    "@testset \"dynamics from random init lands in pattern at low alpha\" begin\n",
    "    # THIS IS TRUE ONLY FOR SMALL N, AT LARGE N IT GETS \n",
    "    # TRAPPED BY SPURIOUS MINIMA\n",
    "    N = 100\n",
    "    α = 0.1\n",
    "    ξ, J = generate_hopfield_sample(N, α)\n",
    "    σ0 = rand([-1,1], N)\n",
    "    σ = run_mcmc(J; σ0, nsweeps=100, infotime=100)\n",
    "    \n",
    "    m0 = staggered_mags(ξ, σ0)\n",
    "    m = staggered_mags(ξ, σ)\n",
    "    μ0max = argmax(m0) \n",
    "    μmax = argmax(m)\n",
    "    mmax = m[μmax]\n",
    "    @test mmax > 0.95\n",
    "    # @test μ0max == μmax # not generally true\n",
    "end\n",
    "\n",
    "@testset \"perturb_config\" begin\n",
    "    N = 100\n",
    "    σ = rand_config(N)\n",
    "    σnew = perturb_config(σ, 0)\n",
    "    @test σnew == σ\n",
    "\n",
    "    for p in [0.1, 0.5, 0.9]\n",
    "        σnew = perturb_config(σ, p)\n",
    "        @test 1-2p - 0.1 < staggered_mag(σ, σnew) < 1-2p + 0.1\n",
    "    end\n",
    "\n",
    "    σnew = perturb_config(σ, 1)\n",
    "    @test σnew == -σ\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
